/*
 * Copyright (C) 2013 TopCoder Inc., All Rights Reserved.
 */
 package gov.opm.scrd.rules


import gov.opm.scrd.entities.application.ExtendedServicePeriod;
import gov.opm.scrd.entities.application.ServicePeriod;

import java.math.BigDecimal;

import java.util.Arrays;
import java.util.ArrayList;
import java.util.Date;
import java.util.List;

/**
 * <p>
 * This is the DSLR rule resource for deduction calculation.
 * This rules are applied to FERS.
 * </p> 
 * <p>
 * <b>Thread Safety:</b>
 * Thread safety is not an issue for drools rules.
 * </p>
 * <p>
 * Version 1.1 - OPM - Rules Engine - Scenarios Conversion 2 - Deduction Update Assembly 
 * <ul>
 * <li>Removed validation related rules (they're moved to deduction_validation.dslr).</li>
 * <li>Almost completely rewrote original rules to general FERS deduction calculation rules.</li>
 * </ul>
 * </p>
 * 
 * @author albertwang, TCSASSEMBLER
 * @version 1.1
 *
 * @since  OPM - Implement Business Rules Engine Deduction Calculation Assembly v1.0
 */

/**
 * Represents the expander for the rules
 */
expander common_deduction.dsl


/**
 * Represents the ExtendedServicePeriod that is used for outputting calculation result
 */
 global List<ExtendedServicePeriod> extendedServicePeriods;
 
/**
 * Represents the service period split dates
 */
global List<Date> servicePeriodSplitDates;


/**
 * Rule to calculate earnings for EARNINGS period.
 */
rule "FERS Deposit EARNINGS Earnings Calculation"
    ruleflow-group "Calculate Earnings"
    when
        There is a valid Service Period that has retirement type as "FERS" and has period type as "DEPOSIT" and has pay type as "EARNINGS"
    then
        Calculate earnings in service period as the amount
end

/**
 * Rule to calculate earnings for ANNUAL SALARY period.
 */
rule "FERS Deposit ANNUAL SALARY Earnings Calculation"
    ruleflow-group "Calculate Earnings"
    when
        There is a valid Service Period that has retirement type as "FERS" and has period type as "DEPOSIT" and has pay type as "ANNUAL SALARY"
    then
        Calculate earnings in service period for annual salary
end

/**
 * Rule to calculate earnings for MONTHLY SALARY period.
 */
rule "FERS Deposit MONTHLY SALARY Earnings Calculation"
    ruleflow-group "Calculate Earnings"
    when
        There is a valid Service Period that has retirement type as "FERS" and has period type as "DEPOSIT" and has pay type as "MONTHLY SALARY"
    then
        Calculate earnings in service period for monthly salary
end

/**
 * Rule to calculate earnings for GS hourly rate service periods before 3-1-1986.
 * The hourly rate multiplier is 2080.
 */
rule "FERS GS Hourly Rate Earnings Calculation Before 3-1-86"
    ruleflow-group "Calculate Earnings"
    when
        There is a valid Service Period that has retirement type as "FERS" and has period type as "DEPOSIT" and has pay type as "HOURLY RATE" and has service type as "GS" and is before "01-Mar-1986"
    then
        Calculate earnings in service period for hourly rate with multiplier 2080
end

/**
 * Rule to calculate earnings for GS hourly rate service periods after 3-1-1986.
 * The hourly rate multiplier is 2087. 
 */
rule "FERS GS Hourly Rate Earnings Calculation After 3-1-86"
    ruleflow-group "Calculate Earnings"
    when
        There is a valid Service Period that has retirement type as "FERS" and has period type as "DEPOSIT" and has pay type as "HOURLY RATE" and has service type as "GS" and is after "01-Mar-1986"
    then
        Calculate earnings in service period for hourly rate with multiplier 2087
end

/**
 * Rule to calculate earnings for WAGE GRADE hourly rate service periods before 3-1-1986.
 * The hourly rate multiplier is 2080.
 */
rule "FERS WAGE GRADE Hourly Rate Earnings Calculation Before 3-1-86"
    ruleflow-group "Calculate Earnings"
    when
        There is a valid Service Period that has retirement type as "FERS" and has period type as "DEPOSIT" and has pay type as "HOURLY RATE" and has service type as "WAGE GRADE" and is before "01-Mar-1986"
    then
        Calculate earnings in service period for hourly rate with multiplier 2080
end

/**
 * Rule to calculate earnings for WAGE GRADE hourly rate service periods after 3-1-1986.
 * The hourly rate multiplier is 2087. 
 */
rule "FERS WAGE GRADE Hourly Rate Earnings Calculation After 3-1-86"
    ruleflow-group "Calculate Earnings"
    when
        There is a valid Service Period that has retirement type as "FERS" and has period type as "DEPOSIT" and has pay type as "HOURLY RATE" and has service type as "WAGE GRADE" and is after "01-Mar-1986"
    then
        Calculate earnings in service period for hourly rate with multiplier 2087
end

/**
 * Rule to calculate earnings for DC Government hourly rate service periods.
 * The hourly rate multiplier is 2080. 
 */
rule "FERS DC Government Hourly Rate Earnings Calculation"
    ruleflow-group "Calculate Earnings"
    when
        There is a valid Service Period that has retirement type as "FERS" and has period type as "DEPOSIT" and has pay type as "HOURLY RATE" and has service type as "DC GOVERNMENT"
    then
        Calculate earnings in service period for hourly rate with multiplier 2080
end

/**
 * Rule to calculate earnings for NO EARNINGS service periods.
 */
rule "FERS NO EARNINGS Earnings Calculation"
    ruleflow-group "Calculate Earnings"
    when
        There is a valid Service Period that has retirement type as "FERS" and has period type as "NO EARNINGS" and has appointment type as "INTERMITTENT"
    then
        Set earnings in service period to 0
end

/**
 * Rule to calculate earnings for LWOP service periods.
 */
rule "FERS LWOP Earnings Calculation"
    ruleflow-group "Calculate Earnings"
    when
        There is a valid Service Period that has retirement type as "FERS" and has period type as "LWOP"
    then
        Set earnings in service period to 0
end

/**
 * Rule to calculate earnings for Postal Part Time Flex hourly rate service periods after 7-25-1985.
 * The hourly rate multiplier is 2000.
 */
rule "FERS Postal Part Time Flex Hourly Rate Earnings Calculation After 7-25-85"
    ruleflow-group "Calculate Earnings"
    when
        There is a valid Service Period that has retirement type as "FERS" and has period type as "DEPOSIT" and has pay type as "HOURLY RATE" and has service type as "POSTAL PART TIME FLEX" and is after "25-Jul-1985"
    then
        Calculate earnings in service period for hourly rate with multiplier 2000
end

/**
 * Rule to calculate earnings for Postal Limited Tour hourly rate service periods.
 * The hourly rate multiplier is 2080.
 */
rule "FERS Postal Limited Tour Hourly Rate Earnings Calculation"
    ruleflow-group "Calculate Earnings"
    when
        There is a valid Service Period that has retirement type as "FERS" and has period type as "DEPOSIT" and has pay type as "HOURLY RATE" and has service type as "POSTAL LIMITED TOUR"
    then
        Calculate earnings in service period for hourly rate with multiplier 2080
end


/**
 * Rule to calculate deduction for NO EARNINGS service periods.
 */
rule "FERS NO EARNINGS Deduction Calculation"
    ruleflow-group "Calculate Deduction"
    when
        There is a valid Service Period that has retirement type as "FERS" and has period type as "NO EARNINGS" and has appointment type as "INTERMITTENT"
    then
        Set deduction in service period to 0
end

/**
 * Rule to calculate deduction for LWOP service periods.
 */
rule "FERS LWOP Deduction Calculation"
    ruleflow-group "Calculate Deduction"
    when
        There is a valid Service Period that has retirement type as "FERS" and has period type as "LWOP"
    then
        Set deduction in service period to 0
end

/**
 * Rule to calculate deduction for Conner Case.
 * The deduction rate is 1.3%.
 */
rule "FERS Conner Case Deduction Calculation"
    ruleflow-group "Calculate Deduction"
    when
        There is a valid Service Period that has retirement type as "FERS" and has period type as "DEPOSIT" and is Conner Case
    then
        Calculate deduction in service period at deduction rate 0.013
end


/**
 * Rule to merge deposit extended service period.
 */
rule "FERS Deposit Continue Extended Service Period"
    ruleflow-group "Continue Extended Service Period"
    when
        There is a valid Service Period that has retirement type as "FERS" and has period type as "DEPOSIT"
    then
        Continue Extended Service Period with no more than 3 days break
end

/**
 * Rule to merge NO EARNINGS extended service period.
 */
rule "FERS NO EARNINGS Continue Extended Service Period"
    ruleflow-group "Continue Extended Service Period"
    when
        There is a valid Service Period that has retirement type as "FERS" and has period type as "NO EARNINGS" and has appointment type as "INTERMITTENT"
    then
        Continue Extended Service Period with no more than 3 days break
end

/**
 * Rule to merge LWOP extended service period.
 */
rule "FERS LWOP Continue Extended Service Period"
    ruleflow-group "Continue Extended Service Period"
    when
        There is a valid Service Period that has retirement type as "FERS" and has period type as "LWOP"
    then
        Continue Extended Service Period with no more than 3 days break
end